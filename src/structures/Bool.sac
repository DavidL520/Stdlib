/*****************************************************************************
 *
 * file:   Bool.sac
 *
 * prefix: ---
 *
 * description:
 *
 *   This module implementation is part of the SAC standard library.
 *
 *   It implements some primitives on boolean arguments that are not
 *   hard-wired into the compiler.
 *
 *
 *****************************************************************************/

module Bool;

use ArrayBasics: {shape, sel};
use ArrayArith: {*};
use ScalarArith: {|,&};

export all;


/*
 *  Type conversion operations.
 */

inline int toi( bool b)
{
  if( b) {
    res = 1;
  } else {
    res = 0;
  }
  return( res);
}

inline bool tob( int i)
{ 
 if ( _eq_(1,i) )
	res = true;
 else if ( _eq_(0,i))
	res = false;
 else
	res = false; /* Should be domain error!! */
 return( res);
}
	
inline bool tob( float i)
{ 
 if ( _eq_(1f,i) )
	res = true;
 else if ( _eq_(0f,i))
	res = false;
 else
	res = false; /* Should be domain error!! */
 return( res);
}

inline bool tob( double i)
{ 
 if ( _eq_(1.0,i) )
	res = true;
 else if ( _eq_(0.0,i))
	res = false;
 else
	res = false; /* Should be domain error!! */
 return( res);
}



/*
 * Reduction operations
 */

inline bool any( bool[*] a)
{
  res = with (0*shape(a) <= iv < shape(a))
        fold(| , false, a[iv]);
  
  return( res);
}


inline bool all( bool[*] a)
{
  res = with (0*shape(a) <= iv < shape(a))
        fold( &, true, a[iv]);
  
  return( res);
}
